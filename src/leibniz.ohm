Leibniz {
  Program = Stmt+

  Stmt = PrintStmt
       | VarDec
       | IfStmt
       | AssignmentStmt
       | id "++;" --increment  

  VarDec = let id "=" Exp ";"
  PrintStmt = print "(" Exp ");"
  IfStmt = if Exp Block (else (Block | IfStmt))?
  AssignmentStmt = id "=" Exp ";"
  Block = "{" Stmt+ "}"

  Exp = Condition relop Condition --test
      | Condition
  Condition = Exp "+" Term --add
            | Exp "-" Term --sub
            | Term
  Term = Term "*" Factor --mul
       | Term "/" Factor --div
       | Term "%" Factor --mod
       | Factor
  Factor = Primary ("**" Factor)? --exp
         | "-" Primary --neg
         | Primary
  Primary = numeral
          | boolean
          | stringlit
          | id
          | FunctionCall
          | "[" ListOf<Exp, ","> "]" --array
          | "(" Exp ")" --parens

  boolean = "true" | "false"

  ExpList = ListOf<Exp, ",">

  FunctionCall = (cos | sin | tan | arccos | arcsin | arctan | sqrt | log | distance) "(" ExpList? ")"

  print = "print" ~idchar
  if = "if" ~idchar
  else = "else" ~idchar
  let = "let" ~idchar

  cos = "cos" ~idchar
  sin = "sin" ~idchar
  tan = "tan" ~idchar
  arccos = "arccos" ~idchar
  arcsin = "arcsin" ~idchar
  arctan = "arctan" ~idchar
  sqrt = "sqrt" ~idchar
  log = "log" ~idchar
  e = "e" ~idchar
  pi = "pi" ~idchar
  distance = "distance" ~idchar

  keyword = print | if | else | let | e | pi | distance 
           | cos | sin | tan | arccos | arcsin | arctan | sqrt | log
           
  id = ~keyword letter idchar*
  idchar = letter | digit | "_"
  numeral = digit+ ("." digit+)? (("E"|"e") ("+"|"-")? digit+)?
  stringlit = "\"" (~"\"" any)* "\""
  
  relop = "<=" | "<" | "==" | "!=" | ">=" | ">"
  
  space += "//" (~"\n" any)* --comment
}
